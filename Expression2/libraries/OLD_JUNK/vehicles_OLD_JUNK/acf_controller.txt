@name ACF Controller Lib 1.0
@persist [Pod]:wirelink [Engine Gearbox DiffFront DiffRear]:entity [LastSwitch Gear BrakeForce BrakeRatio GearLocked ShiftSpeed LastLockKeyVal]:normal [LockKey]:string
@outputs Throttle

function initACF(PodLink:wirelink, EngineEnt:entity, GearboxEnt:entity, DiffFrontEnt:entity, DiffRearEnt:entity, TotalBrakeForce, FrontRearBrakeRatio, ShiftSpeedTime)
{
    Pod = PodLink
    Engine = EngineEnt
    Gearbox = GearboxEnt
    DiffFront = DiffFrontEnt
    DiffRear = DiffRearEnt
    BrakeForce = TotalBrakeForce
    BrakeRatio = FrontRearBrakeRatio
    ShiftSpeed = ShiftSpeedTime
    LockKey = "R"
    
    Gear = 1
}

function handleACF()
{
    Active = Pod["Active", number]
    W = Pod["W", number]
    A = Pod["A", number]
    S = Pod["S", number]
    D = Pod["D", number]
    Shift = Pod["Shift", number]
    Space = Pod["Space", number]
    LockKeyVal = Pod[LockKey, number]
    Alt = Pod["Alt", number]
    
    RPM = Engine:acfRPM()
    ShiftRPMMax = Engine:acfPowerbandMax()
    ShiftRPMMin = Engine:acfPowerbandMin()
    MaxGears = Gearbox:acfNumGears()
    
    Throttle = (W | S ? 60+(Shift*40) : 0)
    Clutch = (Space | Alt ? 100 : 0)
    BrakeFront = BrakeForce*clamp(BrakeRatio, 0, 1)*Space
    BrakeRear = BrakeForce*(1-clamp(BrakeRatio, 0, 1))*(Space | Alt)
    
    GearLocked = (LockKeyVal & LockKeyVal != LastLockKeyVal ? !GearLocked : GearLocked)
    LastLockKeyVal = LockKeyVal
    
    Gear = (S ? MaxGears : Gear)
    Gear = (W & (Gear == MaxGears | Gear == 0) ? 1 : Gear)
    
    if((Space | Alt)) { Gear = (S ? MaxGears : 1), LastSwitch = curtime()+ShiftSpeed }
    if(!Space & !GearLocked)
    {
        if(W & RPM > ShiftRPMMax & Gear < MaxGears-1 & curtime() >= LastSwitch)
        {
            Gear += 1
            LastSwitch = curtime()+ShiftSpeed
        }elseif(W & RPM < ShiftRPMMin & Gear > 1 & curtime() >= LastSwitch)
        {
            Gear -= 1
            LastSwitch = curtime()+ShiftSpeed
        }
    }
    
    Engine:acfActive(Active)
    Engine:acfThrottle(Throttle)
    Gearbox:acfShift(Gear)
    Gearbox:acfClutch(Clutch)
    DiffFront:acfBrake(BrakeFront)
    DiffRear:acfBrake(BrakeRear)
}
